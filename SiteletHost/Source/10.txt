module Factorial =
    open WebSharper

    /// Client-side code.
    [<JavaScript>]
    module private Client =
        open WebSharper.Html.Client

        /// Computes the factorial of a number using recursion and pattern matching.
        let rec factRec n =
            match n with
            | _ when n < 2 -> 1
            | _ -> n * factRec (n - 1)

        /// Computes the factorial of a number using the fold function from the Array module.
        let factFold n = Array.fold (*) 1  [|1 .. n|]

        let input =
            Input [
                Type "text"
                AutoFocus "autofocus"
                Class "form-control"
                Id "factorial"
            ]
        let recSpan = Span []
        let foldSpan = Span []
        let button =
            Button [
                Text "Factorial"
                Class "btn btn-primary"
                Style "margin-left: 8px;"
            ]
            |>! OnClick (fun _ _ ->
                let v = int input.Value
                recSpan.Text <- "Recursion: " + string (factRec v)
                foldSpan.Text <- "Array.fold: " + string (factFold v))

        let main() =
            Div [
                Div [Class "form-inline"]
                    -< [input; button]
                Div [Style "margin-top: 8px;"]
                    -< [recSpan; Br []; foldSpan]
            ]

    /// A control for serving the main pagelet.
    type Control() =
        inherit Web.Control()

        [<JavaScript>]
        override this.Body = Client.main() :> _