module RPC =
    open WebSharper

    /// Server-side code.
    module private Server =
        open System.Security.Cryptography
        open System.Text

        /// Computes the MD5 hash of a string.
        [<Rpc>]
        let md5 (data:string) =
            async {
                use md5 = MD5.Create()
                let sb = StringBuilder()
                data.ToCharArray()
                |> Encoding.Unicode.GetBytes
                |> md5.ComputeHash
                |> Array.iter (fun b -> sb.Append(b.ToString("X2")) |> ignore)
                return sb.ToString()
            }

    /// Client-side code.
    [<JavaScript>]
    module private Client =
        open WebSharper.Html.Client

        let main() =
            let output = Div [Style "margin-top: 8px;"]
            let input =
                Input [
                    Type "text"
                    AutoFocus "autofocus"
                    Class "form-control"
                    Id "display"
                ]
            Div [Class "form-inline"] -< [
                input
                Button [Text "MD5"; Class "btn btn-primary"; Style "margin-left: 8px;"]
                // Click event listener to invoke the md5 server-side function asynchronously.
                |>! OnClick (fun _ _ ->
                    async {
                        let! md5 = Server.md5 input.Value
                        output.Text <- md5
                    } |> Async.Start)                        
                output]
    
    /// A control for serving the main pagelet.              
    type Control() =
        inherit Web.Control()

        [<JavaScript>]
        override __.Body = Client.main() :> _