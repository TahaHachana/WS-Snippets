module WebSocket =
    open WebSharper

    /// Client-side code.
    [<JavaScript>]
    module Client =
        open WebSharper.JavaScript
        open WebSharper.Html.Client

        /// Appends a <p> element containing the specified text to the DOM.
        let log text color =
            P [Text text; Style <| "color: " + color]
            |> (fun p -> ById("ws-log").AppendChild p.Dom)
            |> ignore

        /// Appends a button to the element with the specified id.
        let append id (btn : Element) =
            ById(id).AppendChild(btn.Dom)
            |> ignore

        /// Handles WebSocket events.
        let handleEvents (ws : WebSocket) disconnectBtn sendBtn =
            ws.Onerror <- (fun () -> log "Error" "red")
            ws.Onmessage <- (fun msg -> log ("Received: " + msg.Data.ToString()) "blue")
            ws.Onopen <- (fun () ->
                append "send-btn" sendBtn
                append "btns" disconnectBtn
                log "Connected" "green")
            ws.Onclose <- (fun () ->
                ById("connect-btn").RemoveAttribute "disabled"
                sendBtn.Remove()
                disconnectBtn.Remove()
                log "Disconnected" "rgb(250, 167, 50)")

        /// Creates a WebSocket connection and triggers its event handling.
        let connect (msgText : Element) =
            ById("connect-btn").SetAttribute("disabled", "disabled")
            let ws = WebSocket "ws://echo.websocket.org"
            let sendBtn =
                Button [Text "Send"; Class "btn btn-primary"]
                |>! OnClick (fun _ _ ->
                    let txt = msgText.Value
                    ws.Send txt
                    log ("Sent: " + txt) "black")
            let disconnectBtn =
                Button [Text "Disconnect"; Class "btn btn-warning"]
                |>! OnClick (fun _ _ -> ws.Close())
            handleEvents ws disconnectBtn sendBtn

        let main() =
            let msgText = TextArea [Text "Hello WebSocket"; Id "msg"; Class "form-control"]
            let logDiv = Div [Id "ws-log"]
            Div [Class "row"] -< [
                Div [Class "col-lg-4"] -< [
                    Div [Style "margin-bottom: 10px;"; Id "btns"] -< [
                        Button [Text "Connect"; Id "connect-btn"; Class "btn btn-success"; Style "margin-right: 10px;"]
                        |>! OnClick (fun _ _ -> connect msgText)
                    ]
                    Div [Class "form-group"] -< [
                        Label [Text "Message:"; Style "font-weight: bold;"]
                        msgText
                    ]
                    Div [Id "send-btn"]
                ]
                Div [Class "col-lg-5"; Style "border-left: 1px solid lightgray;"] -< [
                    Div [Style "margin-left: 20px;"] -< [
                        Label [Text "Log:"; Style "font-weight: bold;"]
                        logDiv
                        Button [Text "Clear"; Style "margin-top: 10px;"; Class "btn btn-default"]
                        |>! OnClick (fun _ _ -> logDiv.Html <- "")
                    ]
                ]
            ]

    /// A control for serving the main pagelet.
    type Control() =
        inherit Web.Control()

        [<JavaScript>]
        override __.Body = Client.main() :> _